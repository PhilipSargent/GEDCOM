*| Take a GEDCOM file in which TIME has been written at the same lexical
*| level as DATE in level 1 event structures, and transform so that each
*| TIME tag is at the correct lexical level, and immediately follows the
*| DATE tag in the same structure.

*| Usage: Time2to3 -i <infile> [-o <outfile>]

*| Actually -o <outfile> is completely ignored and output is RAM:NewTree :-(

version$ = "0.02"
vdate$ = "2000-12-19"

SYS "OS_GetEnv" TO Cmd$

in$=FNArg("-i","")
IF in$="" ERROR 1,"Usage: Time2to3 -i <infile> [-o <outfile>]:END
PRINT "TIME2to3 v ";version$;" ";vdate$
out$=FNArg("-o",in$)
I%=0:O%=0
ON ERROR REPORT:PRINT;" at line ";ERL:ON ERROR OFF:PROCtidy:END
I%=OPENIN(in$)
IF I%=0 ERROR 0,"Can't open file "+in$
O%=OPENOUT"RAM:NewTree"

DIM Line$(200)

Line$ = GET$#I%
WHILE NOT EOF#I%
 CASE Line$ OF
  WHEN "1 BIRT","1 CHR ","1 BAPM","1 DEAT","1 CREM","1 BURI","1 MARR","1 MARL","1 ENGA","1 DIV "
    BPUT#O%,Line$
    J%=0
    Line$(J%) = GET$#I%
    WHILE LEFT$(Line$(J%),1)>"1"
      J%+=1
      Line$(J%) = GET$#I%
    ENDWHILE
    REM now have all the lines for that event, plus the next line
    IF J%>0 THEN
      D%=-1:T%=-1
      FOR K%=0 TO J%-1
        IF INSTR(Line$(K%),"2 DATE") D%=K%
        IF INSTR(Line$(K%),"2 TIME") T%=K%
      NEXT
      IF T%<>-1 THEN
        LEFT$(Line$(T%),1)="3"
        IF D%=-1 THEN
          IF T%>0 THEN FOR K%=0 TO T%-1:BPUT#O%,Line$(K%):NEXT
          BPUT#O%,"2 DATE"
          IF T%<J% THEN FOR K%=T% TO J%-1:BPUT#O%,Line$(K%):NEXT
        ELSE
          IF T%=D%+1 THEN
            FOR K%=0 TO J%-1:BPUT#O%,Line$(K%):NEXT
          ELSE IF T%<D% THEN
              IF T%>0 THEN FOR K%=0 TO T%-1:BPUT#O%,Line$(K%):NEXT
              BPUT#O%,Line$(D%)
              BPUT#O%,Line$(T%)
              IF T%+1<D% THEN FOR K%=T%+1 TO D%-1:BPUT#O%,Line$(K%):NEXT
              IF D%<J%-1 THEN FOR K%=D%+1 TO J%-1:BPUT#O%,Line$(K%):NEXT
            ELSE
              FOR K%=0 TO D%:BPUT#O%,Line$(K%):NEXT
              BPUT#O%,Line$(T%)
              IF D%+1<T% THEN FOR K%=D%+1 TO T%-1:BPUT#O%,Line$(K%):NEXT
              IF T%<J%-1 THEN FOR K%=T%+1 TO J%-1:BPUT#O%,Line$(K%):NEXT
            ENDIF
          ENDIF
        ENDIF
      ELSE
        REM no TIME to worry about
        FOR K%=0 TO J%-1:BPUT#O%,Line$(K%):NEXT
      ENDIF
    ENDIF
    Line$=Line$(J%)
  OTHERWISE
    BPUT#O%,Line$
    Line$=GET$#I%
  ENDCASE
ENDWHILE
CLOSE#I%
BPUT#O%,Line$
CLOSE#O%
END

DEF FNArg(opt$,default$)
LOCAL I%,arg$
I% = INSTR(Cmd$,opt$)
IF I%=0 THEN =default$
arg$=MID$(Cmd$,I%+1+LEN(opt$))
WHILE LEFT$(arg$)=" ":arg$=RIGHT$(arg$):ENDWHILE
I% = INSTR(arg$," ")
IF I%=0 THEN = arg$
= LEFT$(arg$,I%-1)
